PROCEDURE hammurabi
    PRINT TAB(32);"HAMURABI"
    PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
    PRINT
    PRINT
    PRINT
    PRINT "TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA"
    PRINT "FOR A TEN-YEAR TERM OF OFFICE."
    PRINT
    DIM A,Z,D,I:INTEGER
    D1=0
    P1=0
    Z=0
    P=95
    S=2800
    H=3000
    E=H-S
    Y=3
    A=H/Y
    I=5
    Q=1
210 D=0
215 PRINT
    PRINT
    PRINT "HAMURABI:  I BEG TO REPORT TO YOU,"
    Z=Z+1
    PRINT "IN YEAR ";Z;", ";D;" PEOPLE STARVED, ";I;" CAME TO THE CITY,"
    P=P+I
    IF Q>0 THEN 230
    P=INT(P/2)
    PRINT "A HORRIBLE PLAGUE STRUCK!  HALF THE PEOPLE DIED."
230 PRINT "POPULATION IS NOW ";P
    PRINT "THE CITY NOW OWNS ";A;" ACRES."
    PRINT "YOU HARVESTED ";Y;" BUSHELS PER ACRE."
    PRINT "THE RATS ATE ";FIX(E);" BUSHELS."
    PRINT "YOU NOW HAVE ";S;" BUSHELS IN STORE."
    PRINT
    IF Z=11 THEN 860
    C=INT(10*RND(1))
    Y=C+17
    PRINT "LAND IS TRADING AT ";Y;" BUSHELS PER ACRE."
320 PRINT "HOW MANY ACRES DO YOU WISH TO BUY";
    INPUT Q
    IF Q<0 THEN 850
    IF Y*Q<=S THEN 330
    GOSUB 710
    GOTO 320
330 IF Q=0 THEN 340
    A=A+Q
    S=S-Y*Q
    C=0
    GOTO 400

340 PRINT "HOW MANY ACRES DO YOU WISH TO SELL";
    INPUT Q
    IF Q<0 THEN 850
    IF Q<A THEN 350
    GOSUB 720
    GOTO 340
350 A=A-Q
    S=S+Y*Q
    C=0
400 PRINT
410 PRINT "HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE";
    INPUT Q
    IF Q<0 THEN 850
    REM *** TRYING TO USE MORE GRAIN THAN IS IN SILOS?
    IF Q<=S THEN 430
    GOSUB 710
    GOTO 410

430 S=S-Q
    C=1
    PRINT
440 PRINT "HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED";
441 INPUT D
    IF D=0 THEN 511
442 IF D<0 THEN 850
    REM *** TRYING TO PLANT MORE ACRES THAN YOU OWN?
    IF D<=A THEN 450
    GOSUB 720
    GOTO 440
    REM *** ENOUGH GRAIN FOR SEED?
450 IF INT(D/2)<=S THEN 455
    GOSUB 710
    GOTO 440
    REM *** ENOUGH PEOPLE TO TEND THE CROPS?
455 IF D<10*P THEN 510
    PRINT "BUT YOU HAVE ONLY";P;"PEOPLE TO TEND THE FIELDS!  NOW THEN,"
    GOTO 440
510 S=S-INT(D/2)
511 GOSUB 800
    REM *** A BOUNTIFUL HARVEST!
    Y=C
    H=D*Y
    E=0
    GOSUB 800
    IF INT(C/2)<>C/2 THEN 530
    REM *** RATS ARE RUNNING WILD!!
    E=INT(S/C)
530 S=S-E+H
    GOSUB 800
    REM *** LET'S HAVE SOME BABIES
    I=INT(C*(20*A+S)/P/100+1)
    REM *** HOW MANY PEOPLE HAD FULL TUMMIES?
    C=INT(Q/20)
    REM *** HORROS, A 15% CHANCE OF PLAGUE
    Q=INT(10*(2*RND(1)-.3))
    IF P<C THEN 210
    REM *** STARVE ENOUGH FOR IMPEACHMENT?
    D=P-C
    IF D>.45*P THEN 560
    P1=((Z-1)*P1+D*100/P)/Z
    P=C
    D1=D1+D
    GOTO 215

560 PRINT
    PRINT "YOU STARVED ";D;" PEOPLE IN ONE YEAR!!!"
565 PRINT "DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY"
    PRINT "BEEN IMPEACHED AND THROWN OUT OF OFFICE BUT YOU HAVE"
    PRINT "ALSO BEEN DECLARED NATIONAL FINK!!!!"
    GOTO 990

710 PRINT "HAMURABI:  THINK AGAIN.  YOU HAVE ONLY"
    PRINT S;" BUSHELS OF GRAIN.  NOW THEN,"
    RETURN

720 PRINT "HAMURABI:  THINK AGAIN.  YOU OWN ONLY ";A;" ACRES.  NOW THEN,"
    RETURN

800 C=INT(RND(1)*5)+1
    RETURN

850 PRINT
    PRINT "HAMURABI:  I CANNOT DO WHAT YOU WISH."
    PRINT "GET YOURSELF ANOTHER STEWARD!!!!!"
    GOTO 990

860 PRINT "IN YOUR 10-YEAR TERM OF OFFICE, ";P1;" PERCENT OF THE"
    PRINT "POPULATION STARVED PER YEAR ON THE AVERAGE, I.E. A TOTAL OF"
    PRINT D1;" PEOPLE DIED!!"
    L=A/P
    PRINT "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
    PRINT L;" ACRES PER PERSON."
    PRINT
    IF P1>33 THEN 565
    IF L<7 THEN 565
    IF P1>10 THEN 940
    IF L<9 THEN 940
    IF P1>3 THEN 960
    IF L<10 THEN 960
    PRINT "A FANTASTIC PERFORMANCE!!!  CHARLEMANGE, DISRAELI, AND"
    PRINT "JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!"
    GOTO 990

940 PRINT "YOUR HEAVY-HANDED PERFORMANCE SMACKS OF NERO AND IVAN IV."
    PRINT "THE PEOPLE (REMIANING) FIND YOU AN UNPLEASANT RULER, AND,"
    PRINT "FRANKLY, HATE YOUR GUTS!!"
    GOTO 990

960 PRINT "YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT"
    PRINT "REALLY WASN'T TOO BAD AT ALL. ";INT(P*.8*RND(1));" PEOPLE"
    PRINT "WOULD DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE OUR"
    PRINT "TRIVIAL PROBLEMS."
990 PRINT
    FOR N=1 TO 10
        PRINT CHR$(7);
    NEXT N
    PRINT "SO LONG FOR NOW."
    PRINT
    END
