PROCEDURE slots
    PRINT TAB(30);"SLOTS"
    PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
    PRINT \ PRINT \ PRINT
    REM PRODUCED BY FRED MIRABELLE AND BOB HARPER ON JAN 29, 1973
    REM IT SIMULATES THE SLOT MACHINE.
    PRINT "YOU ARE IN THE H&M CASINO,IN FRONT OF ONE OF OUR"
    PRINT "ONE-ARM BANDITS. BET FROM $1 TO $100."
    PRINT "TO PULL THE ARM, PUNCH THE RETURN KEY AFTER MAKING YOUR BET."
    LET P=0
    DIM FACE(6):STRING[7]
    READ FACE(1),FACE(2),FACE(3),FACE(4),FACE(5),FACE(6)
    DATA " BAR", " BELL", " ORANGE", " LEMON", " PLUM", " CHERRY"
160 PRINT \ PRINT "YOUR BET ";
    INPUT M
    IF M>100 THEN
      PRINT "HOUSE LIMITS ARE $100"
      GOTO 160
    ENDIF
    IF M<1 THEN
      PRINT "MINIMUM BET IS $1"
      GOTO 160
    ENDIF
    M=INT(M)
    GOSUB 870 
    PRINT
    LET X=INT(RND(6)+1)
    LET Y=INT(RND(6)+1)
    LET Z=INT(RND(6)+1)
    PRINT
    PRINT FACE(X);
    GOSUB 910
    PRINT FACE(Y);
    GOSUB 910
    PRINT FACE(Z)

    IF X=Y THEN
      IF Y=Z THEN
        GOSUB 730
      ELSE
        IF Y=1 THEN
          GOSUB 820
        ELSE
          GOSUB 850
        ENDIF
      ENDIF
      GOTO 510
    ENDIF

    IF X=Z THEN
      IF Z=1 THEN
        GOSUB 820
        GOTO 510
      ENDIF
    ENDIF

    IF Y=Z THEN
      IF Z=1 THEN
        GOSUB 820
      ELSE
        GOSUB 850
      ENDIF
      GOTO 510
    ENDIF

    PRINT
    PRINT "YOU LOST."
    LET P=P-M
510 PRINT "YOUR STANDINGS ARE $";P
    INPUT "AGAIN? ",A$
    IF A$="Y" OR A$="y" THEN 160

    PRINT
    IF P<0 THEN
      PRINT "PAY UP!  PLEASE LEAVE YOUR MONEY ON THE TERMINAL."
    ELSE
      IF P=0 THEN
        PRINT "HEY, YOU BROKE EVEN."
      ELSE
        PRINT "COLLECT YOUR WINNINGS FROM THE H&M CASHIER."
      ENDIF
    ENDIF
    END

730 IF Z=1 THEN
      PRINT \ PRINT "***JACKPOT***"
      P=(((100*M)+M)+P)
    ELSE
      PRINT \ PRINT "**TOP DOLLAR**"
      P=(((10*M)+M)+P)
    ENDIF
    PRINT "YOU WON!"
    RETURN

820 PRINT \PRINT "*DOUBLE BAR*"
    P=(((5*M)+M)+P)
    PRINT "YOU WON!"
    RETURN

850 PRINT \ PRINT "DOUBLE!!"
    PRINT "YOU WON!"
    P=(((2*M)+M)+P)
    RETURN

870 FOR Q4=1 TO 10 
    PRINT CHR$(7);
    NEXT Q4
    RETURN

910 FOR T8=1 TO 5
    PRINT CHR$(7);
    NEXT T8
    RETURN

