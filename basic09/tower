PROCEDURE tower
    PRINT TAB(33);"TOWERS"
    PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
    PRINT \PRINT \PRINT
90  PRINT
    REM*** INITIALIZE
    DIM T(7,3):INTEGER
    DIM D,E,M,N,Q,S,X,Y:INTEGER
    E=0
    FOR D=1 TO 7
      FOR N=1 TO 3
        T(D,N)=0
      NEXT N
    NEXT D
    PRINT "TOWERS OF HANOI PUZZLE." \ PRINT
    PRINT "YOU MUST TRANSFER THE DISKS FROM THE LEFT TO THE RIGHT"
    PRINT "TOWER, ONE AT A TIME, NEVER PUTTING A LARGER DISK ON A"
    PRINT "SMALLER DISK."
    PRINT
215 INPUT "HOW MANY DISKS DO YOU WANT TO MOVE (7 IS MAX)? ";S
    PRINT
    M=0
    FOR Q=1 TO 7
      IF Q=S THEN 350
    NEXT Q
    E=E+1
    IF E>2 THEN
      PRINT "ALL RIGHT, WISE GUY, IF YOU CAN'T PLAY THE GAME RIGHT, I'LL"
      PRINT "JUST TAKE MY PUZZLE AND GO HOME.  SO LONG."
      STOP
    ENDIF
    PRINT "SORRY, BUT I CAN'T DO THAT JOB FOR YOU."
    GOTO 215

    REM *** STORE DISKS FROM SMALLEST TO LARGEST
350 PRINT "IN THIS PROGRAM, WE SHALL REFER TO DISKS BY NUMERICAL CODE."
    PRINT "3 WILL REPRESENT THE SMALLEST DISK, 5 THE NEXT SIZE,"
    PRINT "7 THE NEXT, AND SO ON, UP TO 15.  IF YOU DO THE PUZZLE WITH"
    PRINT "2 DISKS, THEIR CODE NAMES WOULD BE 13 AND 15.  WITH 3 DISKS"
    PRINT "THE CODE NAMES WOULD BE 11, 13 AND 15, ETC.  THE NEEDLES"
    PRINT "ARE NUMBERED FROM LEFT TO RIGHT, 1 TO 3.  WE WILL"
    PRINT "START WITH THE DISKS ON NEEDLE 1, AND ATTEMPT TO MOVE THEM"
    PRINT "TO NEEDLE 3."
    PRINT \ PRINT "GOOD LUCK!" \ PRINT
    Y=7 \ D=15
    FOR X=S TO 1 STEP -1
      T(Y,1)=D \ D=D-2 \ Y=Y-1
    NEXT X
    GOSUB 1230
480 PRINT "WHICH DISK WOULD YOU LIKE TO MOVE"; \E=0
    LOOP
    INPUT D
    EXITIF (D-3)*(D-5)*(D-7)*(D-9)*(D-11)*(D-13)*(D-15)=0 THEN
    ENDEXIT
    PRINT "ILLEGAL ENTRY... YOU MAY ONLY TYPE 3,5,7,9,11,13, OR 15."
    E=E+1
    IF E>1 THEN
      PRINT "STOP WASTING MY TIME.  GO BOTHER SOMEONE ELSE."
      STOP
    ENDIF
    ENDLOOP

580 REM *** CHECK IF REQUESTED DISK IS BELOW ANOTHER
    FOR R=1 TO 7
      FOR C=1 TO 3
        IF T(R,C)=D THEN 640
      NEXT C
    NEXT R
640 FOR Q=R TO 1 STEP -1
      IF T(Q,C)=0 THEN 660
      IF T(Q,C)<D THEN
        PRINT "THAT DISK IS BELOW ANOTHER ONE.  MAKE ANOTHER CHOICE."
        GOTO 480
      ENDIF
660 NEXT Q
    E=0
705 INPUT "PLACE DISK ON WHICH NEEDLE? ";N
    IF (N-1)*(N-2)*(N-3)=0 THEN 800
    E=E+1
    IF E>1 THEN
      PRINT "I TRIED TO WARN YOU, BUT YOU WOULDN'T LISTEN."
      PRINT "BYE BYE, BIG SHOT."
      STOP
    ENDIF
    PRINT "I'LL ASSUME YOU HIT THE WRONG KEY THIS TIME.  BUT WATCH IT,"
    PRINT "I ONLY ALLOW ONE MISTAKE."
    GOTO 705
800 FOR R=1 TO 7
      IF T(R,N)<>0 THEN 840
    NEXT R
    GOTO 880

    REM *** CHECK IF DISK TO BE PLACED ON A LARGER ONE
840 IF D<T(R,N) THEN 880
    PRINT "YOU CAN'T PLACE A LARGER DISK ON TOP OF A SMALLER ONE,"
    PRINT "IT MIGHT CRUSH IT!" \ PRINT "NOW THEN, ";
    GOTO 480

    REM *** MOVE RELOCATED DISK
880 FOR V=1 TO 7
      FOR W=1 TO 3
        IF T(V,W)=D THEN 930
      NEXT W
    NEXT V
    REM *** LOCATE EMPTY SPACE ON NEEDLE N
930 FOR U=1 TO 7
      IF T(U,N)<>0 THEN 970
    NEXT U
    U=7
    GOTO 980
     REM *** MOVE DISK AND SET OLD LOCATION TO 0
970  U=U-1
980  T(U,N)=T(V,W) \ T(V,W)=0
     REM *** PRINT OUT CURRENT STATUS
     GOSUB 1230
     REM *** CHECK IF DONE
     M=M+1
     FOR R=1 TO 7
       FOR C=1 TO 2
       IF T(R,C)<>0 THEN
         IF M<=128 THEN 480
         PRINT "SORRY, BUT I HAVE ORDERS TO STOP IF YOU MAKE MORE THAN"
         PRINT "128 MOVES."
         STOP
       ENDIF
       NEXT C
     NEXT R

1120 IF M = 2^S-1 THEN
       PRINT
       PRINT "CONGRATULATIONS!!"
       PRINT
     ENDIF
     PRINT "YOU HAVE PERFORMED THE TASK IN ";M;" MOVES."
     PRINT \ PRINT "TRY AGAIN (YES OR NO)? ";
     LOOP
       INPUT A$
       IF A$="NO" THEN 1390
       IF A$="YES" THEN 90
       PRINT \ PRINT "'YES' OR 'NO' PLEASE? ";
     ENDLOOP

1230 REM *** PRINT SUBROUTINE
     FOR K=1 TO 7
       Z=10
       FOR J=1 TO 3
         IF T(K,J)=0 THEN
           PRINT TAB(Z);"*";
         ELSE
           PRINT TAB(Z-INT(T(K,J)/2));
           FOR V=1 TO T(K,J)
             PRINT "*";
           NEXT V
         ENDIF
         Z=Z+21
       NEXT J
       PRINT
     NEXT K
     RETURN

1390 PRINT
     PRINT "THANKS FOR THE GAME!"
     PRINT
     END
