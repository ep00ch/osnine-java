PROCEDURE hexapawn
PRINT TAB(32);"HEXAPAWN"
PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
PRINT \ PRINT \ PRINT
REM  HEXAPAWN:  INTERPRETATION OF HEXAPAWN GAME AS PRESENTED IN
REM  MARTIN GARDNER'S "THE UNEXPECTED HANGING AND OTHER MATHEMATIC-
REM  AL DIVERSIONS", CHAPTER EIGHT:  A MATCHBOX GAME-LEARNING MACHINE
REM  ORIGINAL VERSION FOR H-P TIMESHARE SYSTEM BY R.A. KAAPKE 5/5/76
REM  INSTRUCTIONS BY JEFF DALTON
REM  CONVERSION TO MITS BASIC BY STEVE NORTH
REM  CONVERSION TO BASIC09 BY SOREN ROUG

DIM B(19,9),M(19,4),S(9),T(9),FNR(9):INTEGER
DIM P$:STRING[3]
DIM M1,M2,I,J,K,W,L:INTEGER
W=0 \ L=0
P$="X.O"
DATA 3,2,1,6,5,4,9,8,7
FOR I=1 TO 9
  READ FNR(I)
NEXT I
FOR I=1 TO 19
  FOR J=1 TO 9
    READ B(I,J)
  NEXT J
NEXT I
FOR I=1 TO 19
  FOR J=1 TO 4
    READ M(I,J)
  NEXT J
NEXT I
REPEAT
  PRINT "INSTRUCTIONS (Y-N)";
  INPUT A$
  A$=LEFT$(A$,1)
  EXITIF A$="Y" THEN GOSUB 2000 \ ENDEXIT
UNTIL A$="N"
LOOP
X=0
Y=0
S(4)=0
S(5)=0
S(6)=0
S(1)=-1
S(2)=-1
S(3)=-1
S(7)=1
S(8)=1
S(9)=1
GOSUB 1000
120 PRINT "YOUR MOVE";
INPUT M1,M2
IF M1>0 AND M1<10 AND M2>0 AND M2<10 THEN 130
PRINT "ILLEGAL CO-ORDINATES."
GOTO 120
130 IF S(M1)=1 THEN 150
140 PRINT "ILLEGAL MOVE."
GOTO 120
150 IF S(M2)=1 THEN 140
IF M2-M1<>-3 AND S(M2)<>-1 THEN 140
IF M2>M1 THEN 140
IF M2-M1=-3 AND (S(M2)<>0) THEN 140
IF M2-M1<-4 THEN 140
IF M1=7 AND M2=3 THEN 140
S(M1)=0
S(M2)=1
GOSUB 1000
IF S(1)=1 OR S(2)=1 OR S(3)=1 THEN 820
FOR I=1 TO 9
  IF S(I)=-1 THEN 230
NEXT I
GOTO 820
230 FOR I=1 TO 9
  IF S(I)<>-1 THEN 330
  IF S(I+3)=0 THEN 350
  IF FNR(I)=I THEN 320
  IF I>3 THEN 300
  IF S(5)=1 THEN 350
  GOTO 330
300 IF S(8)=1 THEN 350
  GOTO 330
320 IF S(I+2)=1 OR S(I+4)=1 THEN 350
330 NEXT I
340 GOTO 820

350 FOR I=1 TO 19
  FOR J=1 TO 3
    FOR K=3 TO 1 STEP -1
      T((J-1)*3+K)=B(I,(J-1)*3+4-K)
    NEXT K
  NEXT J
  FOR J=1 TO 9
    IF S(J)<>B(I,J) THEN 460
  NEXT J
  R=0
  GOTO 540

460 FOR J=1 TO 9
    IF S(J)<>T(J) THEN 510
  NEXT J
  R=1
  GOTO 540
510 NEXT I
(* REMEMBER THE TERMINATION OF THIS LOOP IS IMPOSSIBLE
PRINT "ILLEGAL BOARD PATTERN."
STOP

540 X=I
FOR I=1 TO 4
  IF M(X,I)<>0 THEN 600
NEXT I
PRINT "I RESIGN."
GOTO 820
600 Y=INT(RND(1)*4+1)
IF M(X,Y)=0 THEN 600
IF R<>0 THEN 630
PRINT "I MOVE FROM ";FIX(INT(M(X,Y)/10));" TO ";MOD(M(X,Y),10)
S(INT(M(X,Y)/10))=0
S(MOD(M(X,Y),10))=-1
GOTO 640
630 PRINT "I MOVE FROM ";FIX(FNR(INT(M(X,Y)/10)));" TO ";
PRINT FIX(FNR(MOD(M(X,Y),10)))
S(FNR(INT(M(X,Y)/10)))=0
S(FNR(MOD(M(X,Y),10)))=-1

640 GOSUB 1000
IF S(7)=-1 OR S(8)=-1 OR S(9)=-1 THEN 870
FOR I=1 TO 9
  IF S(I)=1 THEN 690
NEXT I
GOTO 870

690 FOR I=1 TO 9
  IF S(I)<>1 THEN 790
  IF S(I-3)=0 THEN 120
  IF FNR(I)=I THEN 780
  IF I<7 THEN 760
  IF S(5)=-1 THEN 120
  GOTO 790
  760 IF S(2)=-1 THEN 120
  GOTO 790
  780 IF S(I-2)=-1 OR S(I-4)=-1 THEN 120
790 NEXT I
PRINT "YOU CAN'T MOVE, SO ";
810 GOTO 870
820 PRINT "YOU WIN."
M(X,Y)=0
L=L+1
850 PRINT "I HAVE WON ";W;" AND YOU ";L;" OUT OF ";L+W;" GAMES."
PRINT
ENDLOOP

870 PRINT "I WIN."
W=W+1
GOTO 850

DATA -1,-1,-1,1,0,0,0,1,1,-1,-1,-1,0,1,0,1,0,1
DATA -1,0,-1,-1,1,0,0,0,1,0,-1,-1,1,-1,0,0,0,1
DATA -1,0,-1,1,1,0,0,1,0,-1,-1,0,1,0,1,0,0,1
DATA 0,-1,-1,0,-1,1,1,0,0,0,-1,-1,-1,1,1,1,0,0
DATA -1,0,-1,-1,0,1,0,1,0,0,-1,-1,0,1,0,0,0,1
DATA 0,-1,-1,0,1,0,1,0,0,-1,0,-1,1,0,0,0,0,1
DATA 0,0,-1,-1,-1,1,0,0,0,-1,0,0,1,1,1,0,0,0
DATA 0,-1,0,-1,1,1,0,0,0,-1,0,0,-1,-1,1,0,0,0
DATA 0,0,-1,-1,1,0,0,0,0,0,-1,0,1,-1,0,0,0,0
DATA -1,0,0,-1,1,0,0,0,0
DATA 24,25,36,0,14,15,36,0,15,35,36,47,36,58,59,0
DATA 15,35,36,0,24,25,26,0,26,57,58,0
DATA 26,35,0,0,47,48,0,0,35,36,0,0,35,36,0,0
DATA 36,0,0,0,47,58,0,0,15,0,0,0
DATA 26,47,0,0,47,58,0,0,35,36,47,0,28,58,0,0,15,47,0,0

REM Print board
1000 PRINT
FOR I=1 TO 3
  FOR J=1 TO 3
    PRINT TAB(10);MID$(P$,S((I-1)*3+J)+2,1);
  NEXT J
  PRINT
NEXT I
PRINT
RETURN

2000 PRINT
PRINT "THIS PROGRAM PLAYS THE GAME OF HEXAPAWN."
PRINT "HEXAPAWN IS PLAYED WITH CHESS PAWNS ON A 3 BY 3 BOARD."
PRINT "THE PAWNS ARE MOVED AS IN CHESS - ONE SPACE FORWARD TO"
PRINT "AN EMPTY SPACE OR ONE SPACE FORWARD AND DIAGONALLY TO"
PRINT "CAPTURE AN OPPOSING MAN.  ON THE BOARD, YOUR PAWNS"
PRINT "ARE 'O', THE COMPUTER'S PAWNS ARE 'X', AND EMPTY "
PRINT "SQUARES ARE '.'.  TO ENTER A MOVE, TYPE THE NUMBER OF"
PRINT "THE SQUARE YOU ARE MOVING FROM, FOLLOWED BY THE NUMBER"
PRINT "OF THE SQUARE YOU WILL MOVE TO.  THE NUMBERS MUST BE"
PRINT "SEPERATED BY A COMMA." \ PRINT
PRINT "THE COMPUTER STARTS A SERIES OF GAMES KNOWING ONLY WHEN"
PRINT "THE GAME IS WON (A DRAW IS IMPOSSIBLE) AND HOW TO MOVE."
PRINT "IT HAS NO STRATEGY AT FIRST AND JUST MOVES RANDOMLY."
PRINT "HOWEVER, IT LEARNS FROM EACH GAME.  THUS, WINNING BECOMES"
PRINT "MORE AND MORE DIFFICULT.  ALSO, TO HELP OFFSET YOUR"
PRINT "INITIAL ADVANTAGE, YOU WILL NOT BE TOLD HOW TO WIN THE"
PRINT "GAME BUT MUST LEARN THIS BY PLAYING."
PRINT
PRINT "THE NUMBERING OF THE BOARD IS AS FOLLOWS:"
PRINT TAB(10);"123"
PRINT TAB(10);"456"
PRINT TAB(10);"789"
PRINT
PRINT "FOR EXAMPLE, TO MOVE YOUR RIGHTMOST PAWN FORWARD,"
PRINT "YOU WOULD TYPE 9,6 IN RESPONSE TO THE QUESTION"
PRINT "'YOUR MOVE ?'.  SINCE I'M A GOOD SPORT, YOU'LL ALWAYS"
PRINT "GO FIRST."
PRINT
RETURN
9999 END
