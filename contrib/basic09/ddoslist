PROCEDURE ddoslist
REM Read the table of content of a Dragon Dos disk
INPUT "Disk name? "; fn$
RUN listtoc(fn$, 1.)

PROCEDURE listtoc
    PARAM filename:STRING
    PARAM sides:REAL
    TYPE dentry = flag:BYTE; name:STRING[8]; ext:STRING[3]; alloc(3,4),last:BYTE
    DIM tocrec: dentry
    DIM fd, recnum:INTEGER
    DIM eod,deleted,contentry:BOOLEAN
    DIM tocloc:REAL
    DIM unused(6):BYTE
    OPEN #fd,filename:READ
    REM sides = 1.
    sectors = 18
    tocloc = sides * 16 * sectors * 256 + 512
    SEEK #fd, tocloc
    recnum = 0
    REPEAT
      GET #fd,tocrec
      RUN fixzeros(tocrec.name)
      eod := LAND(tocrec.flag, 8) <> 0
      deleted := LAND(tocrec.flag, 128) <> 0
      contentry := LAND(tocrec.flag, 1) <> 0
      EXITIF eod THEN ENDEXIT
      IF NOT contentry THEN
        PRINT tocrec.name; "."; tocrec.ext;
        IF deleted THEN PRINT "(deleted)";
        ENDIF
        PRINT
      ENDIF
      recnum = recnum + 1
      IF MOD(recnum, 10) = 0 THEN
        GET #fd,unused
      ENDIF
    UNTIL eod
    CLOSE #fd
    BYE

PROCEDURE fixzeros
    PARAM s(8):BYTE
    DIM i:INTEGER
    FOR i = 1 to 8
      IF s(i) = 0 THEN
        s(i) = 255
      ENDIF
    NEXT I
