PROCEDURE Hangman
REM THE GAME OF HANGMAN
REM 
REM ADAPTED FOR BASIC09 BY T.A. GROVAC
REM Downloaded from OS9 Forum on Compuserve and corrected
REM to add missing lines by Ken Drexler.
REM Revised to accept UPPER and lower case guesses and 
REM use only 2/3 of the words each time the game is played.
REM Also the figure array matrix was enlarged.
REM 
PRINT TAB(32); "HANGMAN"
PRINT \ PRINT 
DIM P$(12,18),L$(20),D$(20),N$(26),CD$:STRING[1]
DIM USED(60):BOOLEAN
DIM MSG:STRING[40]
DIM T1,X,Y:INTEGER
DIM C,M,I,J,N:INTEGER

C=0
N=60 \REM WORD COUNT
RUN Randomize \ REM Set RND from clock
10 FOR I=1 TO 20
  D$(I)="-"
NEXT I
M=0
FOR I=1 TO 26
  N$(I)=""
NEXT I
FOR I=1 TO 12
  FOR J=1 TO 18
    P$(I,J)=" "
  NEXT J
NEXT I
FOR I=1 TO 12
  P$(I,1)="X"
NEXT I
FOR I=1 TO 13
  P$(1,I)="X"
NEXT I
P$(2,13)="X"
IF C >= N-20 THEN
  PRINT "You did all the words!"
  END 
ENDIF

REM CLEAR USAGE ARRAY
IF C=0 THEN
  FOR I=1 TO N
    USED(I)=FALSE
  NEXT I
ENDIF

REM SELECT WORD
REPEAT
  Q=INT(RND(N-1)+1)
UNTIL USED(Q)=FALSE
USED(Q)=TRUE
C=C+1

RESTORE 400
T1=0
FOR I=1 TO Q
  READ A$
NEXT I
L=LEN(A$)
FOR I=1 TO LEN(A$)
  L$(I)=MID$(A$,I,1)
NEXT I

30 PRINT "Here are the letters you used:"
FOR I=1 TO 26
  PRINT N$(I); 
  EXITIF N$(I+1)="" THEN ENDEXIT
  PRINT ","; 
NEXT I
PRINT \ PRINT 
FOR I=1 TO L
  PRINT D$(I); 
NEXT I
PRINT \ PRINT 
INPUT "What is your next letter guess? ",G$
RUN ToUpper(G$)
R=0
FOR I=1 TO 26
  IF N$(I)="" THEN 50
  IF G$=N$(I) THEN 
    PRINT "You guessed that letter before!"
    GOTO 30
  ENDIF 
NEXT I
PRINT "PROGRAM ERROR!! Please run again."
STOP 

50 N$(I)=G$
T1=T1+1
FOR I=1 TO L
  IF L$(I)=G$ THEN
    D$(I)=G$
    R=R+1
  ENDIF
NEXT I
IF R=0 THEN
  M=M+1
  GOTO 140
ENDIF

FOR I=1 TO L
  IF D$(I)="-" THEN 100
NEXT I
PRINT "You found the word!"
GOTO 120

100 PRINT 
FOR I=1 TO L
  PRINT D$(I); 
NEXT I
PRINT  \ PRINT 
INPUT "What is your guess for the word? ",B$
RUN ToUpper(B$)
IF A$=B$ THEN
  PRINT "Right!!  It took you "; T1; " guesses."
  GOTO 120
ENDIF
PRINT "WRONG.  Try another letter."
PRINT 
GOTO 30

120 INPUT "Want another word? ",W$
IF LEFT$(W$,1)="y" OR LEFT$(W$,1)="Y" THEN 10
PRINT "It's been fun.  Bye for now."
END 

140 PRINT \ PRINT 
PRINT "Sorry, that letter isn't in the word."
PRINT
RESTORE 450
FOR I = 1 TO M-1
  READ MSG, SZ
  FOR J = 1 TO SZ
    READ X,Y,CD$
  NEXT J
NEXT I
READ MSG, SZ
PRINT MSG
FOR J = 1 TO SZ
  READ X,Y,CD$
  P$(X,Y) = CD$
NEXT J

FOR I=1 TO 12
  FOR J=1 TO 18
    PRINT P$(I,J); 
  NEXT J
  PRINT 
NEXT I
PRINT 
PRINT 

IF M<>10 THEN 30
PRINT "Sorry, you lose. The word was "; A$
PRINT "You missed that one. Do you "; 
GOTO 120
INPUT "Type YES or NO",Y$
IF Y$="YES" THEN 10

400 DATA "GUM","SIN","FOR","CRY","LUG","BYE","FLY"
DATA "UGLY","EACH","FROM","WORK","TALK","WITH","SELF"
DATA "SORE","EAGLE","BIRD","CLOCK","SISTER","MOTHER","FATHER"
DATA "PUPPY","CAKE","YARD","TREE","APPLE","ARM","LEG","BOAT"
DATA "CAR","DRESS","COAT","TENT","SLEEP","PAPER","SCHOOL","PENCIL"
DATA "TEACHER","BUS","PLAY","FLUTE","BOOK","TEST","CIRCLE","PLAIN"
DATA "DOG","TABLE","PLATE","JUMP","PIANO","DANCE","GOOD","PRETTY"
DATA "HOPE","FAITH","LOVE","TRUST","CREED","FORGIVE","PRAY","SEEK"

450 DATA "First we draw a head",10
DATA 3,12,"-",3,13,"-",3,14,"-",4,11,"(",4,12,"."
DATA 4,14,".",4,15,")",5,12,"-",5,13,"-",5,14,"-"
DATA "Now we draw a body",4
DATA 6,13,"X",7,13,"X",8,13,"X",9,13,"X"
DATA "Next we draw an arm",4
DATA 4,9,"\",5,10,"\",6,11,"\",7,12,"\"
DATA "This time it's the other arm",4
DATA 4,17,"/",5,16,"/",6,15,"/",7,14,"/"
DATA "Now let's draw the right leg",2
DATA 10,12,"/",11,11,"/"
DATA "This time we add the left leg",2
DATA 10,14,"\",11,15,"\"
DATA "Now we put up a hand",1
DATA 3,17,"\"
DATA "Next the other hand",1
DATA 3,9,"/"
DATA "Now we draw one foot",2
DATA 12,16,"\",12,17,"_"
DATA "Here's the other foot -- you're hung!!",2
DATA 12,9,"_",12,10,"/"
END 

PROCEDURE Randomize
DIM SEC,MIN,HOU:STRING[2]
D$=DATE$
SEC=RIGHT$(D$,2)
MIN=MID$(D$,13,2)
HOU=MID$(D$,10,2)
SEED = VAL(HOU)* 3600 + VAL(MIN) * 60 + VAL(SEC)
R = RND(0 - SEED)
END

PROCEDURE ToUpper
PARAM B$:STRING
DIM I:INTEGER
DIM X$:STRING[1]
Z$=""
FOR I=1 TO LEN(B$)
  X$=MID$(B$,I,1)
  IF X$ >= "a" AND X$ <= "z" THEN 
    X$=CHR$(ASC(X$)-32)
  ENDIF 
  Z$=Z$+X$
NEXT I
B$=Z$
